PREFIX rmo:	<http://eatld.et.tu-dresden.de/rmo#>
PREFIX prov:	<http://www.w3.org/ns/prov#> 
PREFIX eatld:	<http://eatld.et.tu-dresden.de/>

SELECT DISTINCT ?s ?p ?o
FROM eatld:r43ples-revisions
WHERE {
	GRAPH ?g1 { ?s ?p ?o. }
	{ 
		<http://test.com/r43ples-dataset-master> rmo:fullGraph ?g1.
	}
	UNION
	{	?rg1 a rmo:Revision.
  	  	?rg1 rmo:deltaRemoved ?g1.
      	FILTER (?rg1 IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>))
    }
	MINUS {
		GRAPH ?gm1 { ?s ?p ?o }
		?rm1 a rmo:Revision.
		?rm1 rmo:deltaAdded ?gm1.
		FILTER (?rm1 IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>))
		MINUS {
			GRAPH ?gm1_old {?s ?p ?o.}
			?rm1_old a rmo:Revision.
			?rm1 prov:wasDerivedFrom+ ?rm1_old.
			?rm1_old rmo:deltaRemoved ?gm1_old.		
			FILTER (?rm1_old IN (<http://test.com/r43ples-dataset-revision-4>, <http://test.com/r43ples-dataset-revision-3>))
		}
	}		
}